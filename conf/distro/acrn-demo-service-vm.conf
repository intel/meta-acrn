require conf/distro/include/acrn-demo.inc

DISTRO .= "-service-vm"
DISTRO_NAME += "(Service VM)"
DISTRO_FEATURES += "ServiceVM"

PREFERRED_PROVIDER_virtual/kernel ?= "linux-intel-acrn-service-vm"
PREFERRED_VERSION_linux-intel-acrn-service-vm ?= "5.15%"

PREFERRED_PROVIDER_libvirt = "acrn-libvirt"
PREFERRED_PROVIDER_libvirt-native = "acrn-libvirt-native"
PREFERRED_PROVIDER_nativesdk-libvirt = "nativesdk-acrn-libvirt"

# acrn-libvirt (v6.1.0) is not supported by python3-docutil v0.17 and higher
# python3-docutils (v0.16) recipe from this layer should be dropped with
# acrn-libvirt upgrade
PREFERRED_VERSION_python3-docutils = "0.16"
PREFERRED_VERSION_python3-docutils-native = "0.16"


# elementpath 4.0.1 has removed 'SYMBOLS' attribute, which causing failure in ACRN 3.1.0
# https://github.com/sissaschool/elementpath/commit/a76aeb88b0d103420239df1d5d001f7d7f56b5c9
#
# misc/config_tools/scenario_config/elementpath_overlay.py", line 24, in CustomParser
#|     SYMBOLS = BaseParser.SYMBOLS | {
#|               ^^^^^^^^^^^^^^^^^^
#| AttributeError: type object 'XPath2Parser' has no attribute 'SYMBOLS'
# Carry python3-elementpath 3.0.2 version locally, until it gets fixed upstream
PREFERRED_VERSION_python3-elementpath = "3.0.2"
PREFERRED_VERSION_python3-elementpath-native = "3.0.2"


# GVT enabling. Service VM has pipe 0, one User VM has the rest.
LINUX_GVT_APPEND ?= "i915.enable_gvt=1 i915.nuclear_pageflip=1 "

APPEND += " ${LINUX_GVT_APPEND}"

EFI_PROVIDER = "grub-efi"
GRUB_BUILDIN:append = " multiboot2 "
WKS_FILE = "${@bb.utils.contains_any("IMAGE_CLASSES", "dm-verity-img", "acrn-bootdisk-dmverity.wks.in", "acrn-bootdisk-microcode.wks.in", d)}"

# Disable auto ethernet DHCP as its handled by ACRN tools
PACKAGECONFIG:remove:pn-systemd-conf = "dhcp-ethernet"
